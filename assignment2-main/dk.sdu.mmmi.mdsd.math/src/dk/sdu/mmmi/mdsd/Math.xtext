grammar dk.sdu.mmmi.mdsd.Math with org.eclipse.xtext.common.Terminals

generate math "http://www.sdu.dk/mmmi/mdsd/Math"

OriginExp:
	expressions+=MathExp*
;

MathExp:
	'var' name=ID '=' exp=Exp
;

Exp:
	PlusOrMinus
;

PlusOrMinus returns Exp:
	MulOrDiv ({Plus.left=current} '+' right=MulOrDiv | {Minus.left=current} '-' right=MulOrDiv)*
;

MulOrDiv returns Exp:
	Primary ({Multiply.left=current} '*' right=Primary | {Divide.left=current} '/' right=Primary)*
;

Primary:
	Number | Parenthesis | VariableUse | Letend
;

Parenthesis:
	'(' exp=Exp ')'
;

Letend:
	'let' name=ID '=' val=Exp 'in' exp=Exp 'end'
;

Number:
	value=INT
;

LetMath:
	MathExp | Letend
;

VariableUse:
	ref = [LetMath]
;